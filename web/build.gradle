/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/tutorial_java_projects.html
 */

plugins {
	id 'org.springframework.boot' version '2.0.0.RELEASE'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'io.spring.dependency-management'

configurations {
	compile.exclude module: 'spring-boot-starter-tomcat'
    mybatisGenerator
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-starter-jetty'
	compile 'org.springframework.boot:spring-boot-starter-mustache'
	compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1")
	compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.45'
	// https://mvnrepository.com/artifact/com.mchange/c3p0
	compile group: 'com.mchange', name: 'c3p0', version: '0.9.5.2'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'


	mybatisGenerator group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.3.5'
	mybatisGenerator group: 'mysql', name: 'mysql-connector-java', version: '5.1.45'
	mybatisGenerator group: 'tk.mybatis', name: 'mapper', version: '3.4.2'
}

mainClassName = 'org.zentao.Main'


task mybatisGenerate {
	group="mybatis"
	description="mybatis generator"
	doLast {
		ant.properties['targetProject'] = projectDir.path
		ant.properties['driverClass'] = "com.mysql.jdbc.Driver"
		ant.properties['connectionURL'] = "jdbc:mysql://localhost:7788/zentao?useUnicode=true&characterEncoding=utf8&useSSL=false"
		ant.properties['userId'] = "zentao"
		ant.properties['password'] = "zentao1024"
		ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
		ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
		ant.properties['modelPackage'] = "org.zentao.entity.gen"
		ant.properties['mapperPackage'] = "org.zentao.mapper.gen"
		ant.properties['sqlMapperPackage'] = "org/zentao/mapper/gen"

		ant.taskdef(
				name: 'mbgenerator',
				classname: 'org.mybatis.generator.ant.GeneratorAntTask',
				classpath: configurations.mybatisGenerator.asPath
		)
		ant.mbgenerator(overwrite: true,
				configfile: 'src/main/resources/mybatis-generator/main.xml', verbose: true) {
			propertyset {
				propertyref(name: 'targetProject')
				propertyref(name: 'userId')
				propertyref(name: 'driverClass')
				propertyref(name: 'connectionURL')
				propertyref(name: 'password')
				propertyref(name: 'src_main_java')
				propertyref(name: 'src_main_resources')
				propertyref(name: 'modelPackage')
				propertyref(name: 'mapperPackage')
				propertyref(name: 'sqlMapperPackage')

			}
		}
	}

}
